
/* 
 * The Gradle task dependencies 
 */
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'io.opencaesar.adapters:oml2owl-gradle:+'
		classpath 'io.opencaesar.owl:owl-load-gradle:+'
		classpath 'io.opencaesar.owl:owl-fuseki:+'
		classpath 'io.opencaesar.owl:owl-fuseki-gradle:+'
		classpath 'io.opencaesar.owl:owl-query-gradle:+'
		classpath 'io.opencaesar.owl:owl-reason-gradle:+'
        classpath 'io.opencaesar.oml:oml-bikeshed-gradle:+'
    }
}

plugins {
  id 'com.palantir.git-version' version '0.12.3'
}

/* 
 * The Maven coordinates for the project artifact
 */
ext.title = "Core Vocabularies"
description='The set of core vocabularies'
group = 'io.opencaesar.ontologies'
version = gitVersion()

def fusekiDataset = 'core-vocabularies'

apply from: "${rootDir}/gradle/maven-deployment.gradle"

/*
 * A task to generate Bikeshed specification for the OML catalog
 */
task omlToBikeshed(type: io.opencaesar.oml.bikeshed.Oml2BikeshedTask) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OWL folder
    outputFolderPath = file('build/bikeshed')
    // Publish URL
    publishUrl = 'https://opencaesar.github.io/core-vocabularies/'
}

/*
 * A task to generate the Bikeshed documentations in HTML
 */
import org.gradle.internal.os.OperatingSystem
task generateDocs(dependsOn: omlToBikeshed) {
    doLast {
        if (OperatingSystem.current().isWindows()) {
            exec { commandLine 'build/bikeshed/publish.bat' }
        } else {
            exec { commandLine 'chmod', '+x', 'build/bikeshed/publish.sh' }
            exec { commandLine 'build/bikeshed/publish.sh' }
        }
    }
}

/*
 * A task to convert the OML catalog to OWL catalog
 */
task omlToOwl(type:io.opencaesar.oml2owl.Oml2OwlTask) {
    // OML catalog
    inputCatalogPath = file('catalog.xml')
    // OWL catalog
    outputCatalogPath = file('build/owl/catalog.xml')
    outputFileExtension = 'trig'
}

/*
 * A task to run the Openllet reasoner on the OWL catalog
 */
task owlReason(type:io.opencaesar.owl.reason.OwlReasonTask, dependsOn: omlToOwl) {
	// OWL catalog
	catalogPath = file('build/owl/catalog.xml')
	// Input ontology IRI to reason on
	inputOntologyIri = 'http://opencaesar.io/bundle1'
    inputFileExtensions = ['trig']
	// Entailment statements to generate and the ontologies to persist them in
	specs = [
		'http://opencaesar.io/bundle1/classes = ALL_SUBCLASS',
		'http://opencaesar.io/bundle1/properties = INVERSE_PROPERTY | ALL_SUBPROPERTY',
		'http://opencaesar.io/bundle1/individuals = ALL_INSTANCE | DATA_PROPERTY_VALUE | OBJECT_PROPERTY_VALUE | SAME_AS'
	]
	// Junit error report
	reportPath = file('build/reports/reasoning.xml')
}

/*
 * A task to load an OWL catalog to a Fuseki dataset endpoint
 */
task owlLoad(type:io.opencaesar.owl.load.OwlLoadTask, dependsOn: owlReason) {
	catalogPath = file('build/owl/catalog.xml')
	endpointURL = "http://localhost:3030/$fusekiDataset"
    fileExtensions = ['trig', 'ttl']
	iris = [
		'http://opencaesar.io/bundle1/classes',
	    'http://opencaesar.io/bundle1/properties',
	    'http://opencaesar.io/bundle1/individuals'
	]
}

/*
 * Start and stop the Fuseki server
 */
task startFuseki(type: io.opencaesar.owl.fuseki.StartFusekiTask, group: 'fuseki') {
	configurationPath = file('.fuseki.ttl')
	outputFolderPath = file('.fuseki')
}

task stopFuseki(type: io.opencaesar.owl.fuseki.StopFusekiTask, group: 'fuseki') {
	outputFolderPath = file('.fuseki')
}

/*
 * Tasks to run a SPARQL query on a Fuseki dataset endpoint
 */
task owlClasses(type:io.opencaesar.owl.query.OwlQueryTask, dependsOn: owlLoad, group: 'oml') {
	endpointURL = "http://localhost:3030/$fusekiDataset"
	queryPath = file('src/sparql/classes.sparql')
	resultPath = file('build/frames')
	format = 'csv'
	outputs.files('build/frames/classes.csv')
}

/*
 * A task to build the project, which executes several tasks together
 */
tasks.named('build') {
    dependsOn owlReason
}

/*
 * Publish artifacts
 */
task omlZip(type:Zip, dependsOn: build, group: 'publishing') {
    from file('src/oml')
    include "**/*.oml"
    destinationDirectory = file('build/libs')
    archiveBaseName = project.name
    archiveVersion = project.version
}

task owlZip(type: Zip, dependsOn: build, group: 'publishing') {
  from file('build/owl')
	include "**/*.trig"
	include "**/*.ttl"
	destinationDirectory = file('build/libs')
	archiveBaseName = project.name
	archiveVersion = project.version
  archiveExtension = 'owl.zip'
}

publishing.publications.maven.artifact omlZip
publishing.publications.maven.artifact owlZip